plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.diffplug.spotless' version '6.11.0'
	id 'jacoco'
}

group = 'com.testcar'
version = '0.0.1-SNAPSHOT'
compileJava.options.encoding = 'UTF-8'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	querydsl.extendsFrom compileClasspath
}

repositories {
	mavenCentral()
}

dependencies {
	// configuration property binding
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// DB
	runtimeOnly 'com.mysql:mysql-connector-j'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// QueryDSL
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// Flyway
	implementation 'org.flywaydb:flyway-mysql'
	implementation 'org.flywaydb:flyway-core'

	// MVC
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// jwt
	implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.11.5'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
	implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'

	// Encryption
	implementation 'org.mindrot:jbcrypt:0.4'

	// Swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}

spotless {
	java {
		target("**/*.java")
		targetExclude("**/generated/**/*.java")
		googleJavaFormat().aosp()
		importOrder()
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
	main.java.srcDirs += [ querydslDir ]
}

tasks.withType(JavaCompile) {
	options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

clean.doLast {
	file(querydslDir).deleteDir()
}

jacoco {
	toolVersion = '0.8.11'
}

jacocoTestReport {
	dependsOn(tasks.test)
	reports {
		html.required = true
		csv.required = true
		xml.required = false
	}

	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect {
					fileTree(dir: it, excludes: [
							'**/*Application*',
							'**/*Exception*',
							'**/*Controller*',
							'**/entity/*Status*',
							'**/entity/*Type*',
							'**/entity/*Role*',
							'**/model/**',
							'**/exception/**',
							'**/repository/**',
							'**/util/**',
							'**/config/**',
							'**/common/**',
							'**/Q*.class',
							'**/*$Enum.class',
					])
				})
		)
	}
}
